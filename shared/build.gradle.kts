/*
 * Copyright 2023 Airthings ASA. All rights reserved.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software
 * and associated documentation files (the “Software”), to deal in the Software without
 * restriction, including without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
 * BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget

internal val jvmTargetVersion: JavaVersion = JavaVersion.VERSION_11

plugins {
    kotlin("multiplatform")
    id("com.android.library")
}

kotlin {
    androidTarget()

    listOf(
        iosX64(),
        iosArm64(),
        iosSimulatorArm64(),
    ).forEach {
        // Fixes this: https://rdr.to/DkdMx1MXyeB
        it.binaries.all {
            linkerOpts += "-ld64"
        }
        it.binaries.framework {
            baseName = rootProject.name
        }
    }

    // Export KDoc comments to generated Objective-C header (https://rdr.to/8KKpSbCUBdY).
    targets.withType<KotlinNativeTarget> {
        compilations["main"].kotlinOptions.freeCompilerArgs += "-Xexport-kdoc"
    }

    sourceSets {
        val commonMain by getting {
            dependencies {
                implementation(
                    "co.touchlab:stately-concurrency:" +
                        "${properties["version.stately.concurrency"]}",
                )
                implementation(
                    "org.jetbrains.kotlinx:kotlinx-coroutines-core:" +
                        "${properties["version.kotlin.coroutines"]}",
                )
                implementation(
                    "org.jetbrains.kotlinx:kotlinx-datetime:" +
                        "${properties["version.kotlin.datetime"]}",
                )
            }
        }
        val iosX64Main by getting
        val iosArm64Main by getting
        val iosSimulatorArm64Main by getting
        val iosMain by creating {
            dependsOn(commonMain)
            iosX64Main.dependsOn(this)
            iosArm64Main.dependsOn(this)
            iosSimulatorArm64Main.dependsOn(this)
        }
    }
}

android {
    namespace = "${rootProject.group}.logging"

    compileSdk = "${properties["build.android.compileSdk"]}".toInt()
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")

    defaultConfig {
        minSdk = "${properties["build.android.minimumSdk"]}".toInt()
        @Suppress("UnstableApiUsage")
        targetSdk = "${properties["build.android.targetSdk"]}".toInt()
    }

    compileOptions {
        sourceCompatibility = jvmTargetVersion
        targetCompatibility = jvmTargetVersion
    }
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = "AirthingsGitHubPackages"
            version = rootProject.version
            url = uri("https://maven.pkg.github.com/airthings/kmplog")

            credentials {
                // The following are automatically generated by GitHub.
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
